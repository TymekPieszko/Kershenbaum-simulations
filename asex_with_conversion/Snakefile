import numpy as np
configfile: "config.yaml"

# OPTIONALLY module load Mamba/4.14.0-0
# CLUSTER EXECUTION
# snakemake --executor slurm -j unlimited --default-resources slurm_account=zool-barralab slurm_partition=devel --use-conda --conda-frontend mamba --rerun-incomplete

# Simulation params
L = config['L']
N = config['N']
TRACT_LEN = config['TRACT_LEN']
GC_RATE_min = config['GC_RATE_min']
GC_RATE_max = config['GC_RATE_max']
GC_RATE_num = config['GC_RATE_num']
GC_RATE_lst = [round(x,11) for x in np.logspace(GC_RATE_min, GC_RATE_max, num=GC_RATE_num)]
# GC_RATE_lst.extend([1.0e-7, 1.0e-8, 1.0e-10])
SEX_FREQ_lst = config['SEX_FREQ_lst']
##############
MUT_RATE = config['MUT_RATE']
S_MEAN = config['S_MEAN']
S_SHAPE = config['S_SHAPE']
##############
REP_NUM = config['REP_NUM'] # one value
REP_lst = list(range(int(REP_NUM))) # create a list
GENS = config['GENS']

simulate_targets = expand("sim_output/ts/SEX_FREQ~{SEX_FREQ}/GC_RATE~{GC_RATE}/{REP}.trees", SEX_FREQ=SEX_FREQ_lst, GC_RATE=GC_RATE_lst, REP=REP_lst)


rule all:
    input:
        simulate_targets

rule simulate:
    output:
        TS_FILE = "sim_output/ts/SEX_FREQ~{SEX_FREQ}/GC_RATE~{GC_RATE}/{REP}.trees",
        FITNESS_FILE = "sim_output/fitness/SEX_FREQ~{SEX_FREQ}/GC_RATE~{GC_RATE}/{REP}.txt",
        GENOTYPE_FILE = "sim_output/genotypes/SEX_FREQ~{SEX_FREQ}/GC_RATE~{GC_RATE}/{REP}.txt",
    conda:
        "slim"
    params:
        L = L,
        N = N,
        TRACT_LEN = TRACT_LEN,
        ##########
        MUT_RATE = MUT_RATE,
        S_MEAN = S_MEAN,
        S_SHAPE = S_SHAPE,
        ##########
        GENS = GENS
    threads: 8
    resources:
        mem_mb=100000, # 100 MB
        runtime=10 # 10 min
    shell:
        "slim "
        "-d L={params.L} "
        "-d N={params.N} "
        "-d TRACT_LEN={params.TRACT_LEN} "
        "-d GENS={params.GENS} "
        "-d SEX_FREQ={wildcards.SEX_FREQ} "
        "-d GC_RATE={wildcards.GC_RATE} "
        "-d MUT_RATE={params.MUT_RATE} "
        "-d S_MEAN={params.S_MEAN} "
        "-d S_SHAPE={params.S_SHAPE} "
        "-d 'FITNESS_FILE=\"{output.FITNESS_FILE}\"' "
        "-d 'GENOTYPE_FILE=\"{output.GENOTYPE_FILE}\"' "
        "-d 'TS_FILE=\"{output.TS_FILE}\"' "
        "scripts/asex_with_conversion_muts_smk.slim"